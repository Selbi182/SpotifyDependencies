buildscript {
    ext {
        // Spring has to stay at 2.x.x for now, as 3.x.x requires Java 17
        springVersion = '2.7.18'
    }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'spotify'
version = '1.11.4ec'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

publishing {
    repositories {
        maven {
            name 'spotify-dependencies'
            url 'https://mymavenrepo.com/repo/dKBgnxLBDZsY8Dh8aCq9/'
            credentials {
                username project.publishingUsername
                password project.publishingPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spotify-dependencies'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'spotify-dependencies'
                description = 'Basic dependencies for all my Spotify projects'
                url = 'https://github.com/Selbi182/SpotifyDependencies'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'selbi'
                        name = 'Selbi'
                        email = 'selbix@web.de'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web:$springVersion"
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:9.1.0'
    implementation 'org.jsoup:jsoup:1.16.2'
    implementation 'com.github.albfernandez:juniversalchardet:2.4.0'
}
